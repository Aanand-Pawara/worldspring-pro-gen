#version 300 es
// Underground levels, painterly (the battlemap's style): smooth floor outlines from a blurred
// floor field (rock walls roughened by noise in caves and mines, near-straight dressed stone in
// built sites), a lit inked rim where the wall meets the floor, deep textured rock beyond,
// ambient occlusion and NW-light shadows on the floor, floors by material (cave rock, basalt,
// mine earth, sewer brick, flagstones), animated sewage or lava channels, ledges as smooth
// contours with their shadows, and the 5-ft grid.
//
// uField: 4 texels per square, linear: r floor (blurred mask), g liquid (blurred), b raised
//         floor (blurred, raise / 10 ft). Built sites (uSquare): nearest, the floor unblurred;
//         their walls are measured from the squares instead, keeping square corners.
// uCells: one texel per square, nearest: r material, b variation, a floor.

in vec2 vUV;
in vec2 vLocal;

out vec4 finalColor;

uniform sampler2D uNoise;
uniform sampler2D uField;
uniform sampler2D uCells;

uniform vec2 uN;
uniform vec2 uLight;
uniform float uTime;
uniform float uRough;
uniform float uSquare;
uniform float uLiquid;
uniform float uGhost;
uniform float uGridAlpha;
// Where this grid sits among its neighbours (squares; a city's sewer sections, so their
// textures run on across the edges), else 0.
uniform vec2 uOrigin;
uniform vec4 uColor;

const vec3 INK = vec3(0.10, 0.085, 0.07);

float hash21(vec2 p) {
    p = fract(p * vec2(123.34, 456.21));
    p += dot(p, p + 45.32);
    return fract(p.x * p.y);
}

float vnoise(vec2 p) {
    vec2 i = floor(p);
    vec2 f = fract(p);
    f = f * f * (3.0 - 2.0 * f);
    return texture(uNoise, (i + f + 0.5) / 512.0).r;
}

float fbm(vec2 p) {
    return 0.55 * vnoise(p) + 0.3 * vnoise(p * 2.13 + 7.1) + 0.15 * vnoise(p * 4.37 + 3.3);
}

vec4 field(vec2 q) {
    return texture(uField, q / uN);
}

// Thin dark seams where noise crosses a level (cracks in rock and stone): about `w` of the
// noise's range wide, never wider than ~1.5 px (so close up they stay ink lines, not bands).
float seam(float v, float w) {
    float px = fwidth(v) * 1.5;
    return 1.0 - smoothstep(0.0, max(min(w, px), 1e-4), abs(v - 0.5));
}

// (`p`: position for noise, continuous from one section of sewers to the next.)
vec3 rockMass(vec2 p, float depth) {
    vec3 c = vec3(0.25, 0.23, 0.21);
    c *= 0.72 + 0.5 * vnoise(p * 0.55) + 0.12 * (vnoise(p * 2.4 + 5.0) - 0.5);
    // Fractures.
    c = mix(c, c * 0.6, seam(vnoise(p * 1.1 + 3.0), 0.03));
    // Deeper into the rock is darker.
    return c * mix(1.0, 0.5, smoothstep(0.0, 2.5, depth));
}

// Built sites: signed distance (squares, + on the floor) to the nearest square of the other
// kind (rock or floor; off the grid is rock), measured square-wise (the larger of the x and
// y gaps), so the wall, its coping and its shadow all keep square corners; and the way from
// the rock toward the floor. Exact within two squares (deeper rock reads as two).
void boxField(vec2 q, out float d, out vec2 n) {
    ivec2 s = clamp(ivec2(floor(q)), ivec2(0), ivec2(uN) - 1);
    bool fl = texelFetch(uCells, s, 0).a > 0.5;
    float best = 2.0;
    vec2 way = vec2(0.0);
    for (int j = -2; j <= 2; j++) {
        for (int i = -2; i <= 2; i++) {
            // (The outer ring is no nearer than one square: only needed when nothing is.)
            if (max(abs(i), abs(j)) == 2 && best < 1.0) continue;
            ivec2 c = s + ivec2(i, j);
            bool inside = all(greaterThanEqual(c, ivec2(0))) && all(lessThan(c, ivec2(uN)));
            bool f = inside && texelFetch(uCells, c, 0).a > 0.5;
            if (f == fl) continue;
            vec2 lo = vec2(c);
            vec2 gap = max(max(lo - q, q - lo - 1.0), 0.0);
            float m = max(gap.x, gap.y);
            if (m < best) {
                best = m;
                way = q - clamp(q, lo, lo + 1.0);
            }
        }
    }
    d = fl ? best : -best;
    n = length(way) > 1e-5 ? normalize(fl ? way : -way) : vec2(0.0);
}

vec3 floorColor(int mat, vec2 q, vec2 p, float var) {
    vec3 c;
    if (mat == 0) {
        // Cave floor: mottled rock in broad patches, fine cracks, grit (relief and pebbles
        // are added for every natural floor).
        c = vec3(0.49, 0.45, 0.40) * (0.84 + 0.24 * vnoise(p * 0.35));
        c = mix(c, c * vec3(0.94, 0.96, 1.0), smoothstep(0.4, 0.7, vnoise(p * 0.25 + 20.0)) * 0.4);
        c = mix(c, c * 0.68, seam(vnoise(p * 0.9 + 4.0), 0.02));
        c = mix(c, c * 0.82, smoothstep(0.8, 0.92, vnoise(p * 6.3 + 9.0)) * 0.5);
    } else if (mat == 1) {
        // Basalt: dark, columnar cracks, glints.
        c = vec3(0.30, 0.28, 0.27) * (0.85 + 0.25 * fbm(p * 0.8));
        c = mix(c, c * 0.45, seam(vnoise(p * 1.9 + 2.0), 0.04));
        c += vec3(0.12) * smoothstep(0.93, 0.98, vnoise(p * 9.0));
    } else if (mat == 2) {
        // Mine floor: packed earth and gravel.
        c = vec3(0.47, 0.39, 0.30) * (0.82 + 0.3 * fbm(p * 0.6));
        c = mix(c, vec3(0.40, 0.38, 0.36), smoothstep(0.75, 0.88, vnoise(p * 6.0)) * 0.6);
        c = mix(c, c * 0.75, smoothstep(0.4, 0.6, vnoise(p * vec2(0.6, 3.0))) * 0.3);
    } else if (mat == 3) {
        // Sewer walkway: running-bond brick, grimy.
        vec2 b = q * vec2(2.0, 4.0);
        b.x += 0.5 * mod(floor(b.y), 2.0);
        vec2 g = fract(b);
        float mortar = 1.0 - smoothstep(0.0, 0.08, min(min(g.x, 1.0 - g.x) * 0.5, min(g.y, 1.0 - g.y)));
        c = vec3(0.50, 0.40, 0.33) * (0.82 + 0.3 * hash21(floor(b)));
        c = mix(c, vec3(0.30, 0.29, 0.26), mortar * 0.8);
        c = mix(c, c * vec3(0.7, 0.75, 0.6), smoothstep(0.45, 0.8, fbm(p * 0.5 + 2.0)) * 0.5);
    } else if (mat == 7) {
        // Marble: a square slab per square, light and dark in turn, veined.
        vec2 cell = floor(q);
        vec2 g = fract(q);
        c = mix(vec3(0.80, 0.78, 0.74), vec3(0.60, 0.58, 0.56), mod(cell.x + cell.y, 2.0)) * (0.94 + 0.08 * hash21(cell));
        c = mix(c, c * 0.72, seam(fbm(p * 0.9 + cell * 0.37), 0.015));
        float joint = 1.0 - smoothstep(0.0, 0.03, min(min(g.x, 1.0 - g.x), min(g.y, 1.0 - g.y)));
        c = mix(c, INK * 1.8, joint * 0.5);
    } else if (mat == 8) {
        // Hewn rock: big blocks two squares long in running bond, tight joints.
        vec2 b = q * vec2(0.5, 1.0);
        b.x += 0.5 * mod(floor(b.y), 2.0);
        vec2 g = fract(b);
        c = vec3(0.52, 0.49, 0.45) * (0.85 + 0.2 * hash21(floor(b))) * (0.92 + 0.12 * vnoise(p * 2.0));
        float joint = 1.0 - smoothstep(0.0, 0.04, min(min(g.x, 1.0 - g.x) * 2.0, min(g.y, 1.0 - g.y)));
        c = mix(c, INK * 1.5, joint * 0.7);
    } else if (mat == 9) {
        // Ice: pale blue, cracked, glinting.
        c = vec3(0.74, 0.84, 0.90) * (0.88 + 0.16 * fbm(p * 0.5));
        c = mix(c, vec3(0.50, 0.64, 0.78), seam(vnoise(p * 1.3 + 2.0), 0.02));
        c += vec3(0.15) * smoothstep(0.94, 0.99, vnoise(p * 8.0));
    } else {
        // Flagstones: rows of irregular stones; boss chambers larger and darker, crypts dusty.
        float scale = mat == 5 ? 0.7 : 1.0;
        vec2 f = q * vec2(1.0, 1.35) * scale;
        float row = floor(f.y);
        f.x += 0.5 * mod(row, 2.0) + 0.17 * sin(row * 3.1);
        vec2 cell = floor(f);
        vec2 g = fract(f);
        float edge = min(min(g.x, 1.0 - g.x) * 1.35, min(g.y, 1.0 - g.y));
        edge += 0.04 * (vnoise(p * 6.0) - 0.5);
        float mortar = 1.0 - smoothstep(0.02, 0.07, edge);
        vec3 stone = mat == 5 ? vec3(0.44, 0.39, 0.37) : mat == 6 ? vec3(0.60, 0.56, 0.48) : vec3(0.58, 0.55, 0.50);
        c = stone * (0.8 + 0.32 * hash21(cell + var)) * (0.9 + 0.15 * vnoise(p * 3.0));
        c = mix(c, INK * 1.6, mortar * 0.75);
    }
    return c;
}

void main() {
    vec2 q = vUV * uN;
    vec2 p = q + uOrigin;
    vec4 here = field(q);
    float d;
    vec2 n;
    if (uSquare > 0.5) {
        boxField(q, d, n);
    } else {
        // Signed distance to the wall (squares, + on the floor) from the blurred floor field.
        float e = 0.25;
        vec2 grad = vec2(field(q + vec2(e, 0.0)).r - field(q - vec2(e, 0.0)).r, field(q + vec2(0.0, e)).r - field(q - vec2(0.0, e)).r) / (2.0 * e);
        d = (here.r - 0.5) / max(length(grad), 0.35);
        // Rock juts out and bites in (caves); dressed stone stays nearly straight.
        d += uRough * ((fbm(p * 0.8 + 3.0) - 0.5) * 0.75 + (vnoise(p * 3.7) - 0.5) * 0.25 + (vnoise(p * 9.0) - 0.5) * 0.08);
        n = length(grad) > 1e-4 ? normalize(grad) : vec2(0.0);
    }
    float aa = max(fwidth(d), 1e-4);

    vec3 col;
    float alpha;
    float rim = mix(0.16, 0.3, uRough);
    if (d < 0.0) {
        // Rock: the wall's top edge lit from the NW, then the rock mass.
        vec3 r = rockMass(p, -d);
        float lit = clamp(0.55 + 0.6 * dot(n, uLight), 0.25, 1.15);
        vec3 lip = rockMass(p * 1.3, 0.0) * 1.55 * lit;
        if (uRough < 0.2) {
            // Dressed stone coping on built walls.
            vec2 b = q * vec2(1.6, 1.6);
            vec2 g = fract(b + 0.5 * mod(floor(b.yx), 2.0));
            float joint = 1.0 - smoothstep(0.0, 0.06, min(min(g.x, 1.0 - g.x), min(g.y, 1.0 - g.y)));
            lip = mix(vec3(0.56, 0.53, 0.48) * (0.85 + 0.25 * hash21(floor(b))) * lit, INK, joint * 0.5);
        }
        col = mix(lip, r, smoothstep(-rim, -rim - 0.12, d));
        alpha = mix(1.0, 0.9, smoothstep(-rim, -rim - 0.5, d));
    } else {
        ivec2 s = clamp(ivec2(floor(q)), ivec2(0), ivec2(uN) - 1);
        vec4 cell = texelFetch(uCells, s, 0) * 255.0;
        if (cell.a < 0.5) {
            // A smoothed edge reaching into a rock square: the floor next to it.
            cell = texelFetch(uCells, clamp(s + ivec2(round(n)), ivec2(0), ivec2(uN) - 1), 0) * 255.0;
        }
        int mat = int(cell.r + 0.5);
        col = floorColor(mat, q, p, cell.b);
        if (mat <= 2) {
            // Natural floors: uneven rock lit from the NW, loose stones, rubble at the wall foot.
            float b0 = vnoise(p * 0.7);
            vec2 bg = vec2(vnoise(p * 0.7 + vec2(0.12, 0.0)) - b0, vnoise(p * 0.7 + vec2(0.0, 0.12)) - b0);
            col *= 1.0 + clamp(dot(bg, uLight) * 3.2, -0.15, 0.15);
            vec2 sc = q * 1.7;
            vec2 cid = floor(sc);
            vec2 off = vec2(hash21(cid), hash21(cid + 7.3)) * 0.6 + 0.2;
            float pr = 0.09 + 0.08 * hash21(cid + 3.1);
            vec2 dv = fract(sc) - off;
            if (hash21(cid + 1.7) > 0.72) {
                float stone = 1.0 - smoothstep(pr - 0.03, pr, length(dv));
                float shade = 1.0 - smoothstep(pr * 0.4, pr + 0.06, length(dv + uLight * pr * 0.6));
                col = mix(col, col * 0.55, (1.0 - smoothstep(pr, pr + 0.07, length(dv - uLight * 0.05))) * 0.6);
                col = mix(col, col * (1.05 + 0.25 * shade), stone);
            }
            float foot = (1.0 - smoothstep(0.05, 0.6, d)) * uRough;
            float rub = smoothstep(0.45, 0.75, vnoise(p * 4.5 + 13.0));
            col = mix(col, mix(col * 0.6, col * 1.2, vnoise(p * 9.0)), foot * rub * 0.8);
        }
        // Raised floors (ledges): lighter tops, inked lips, shadows below them.
        float t = here.b;
        col *= 1.0 + 0.22 * t;
        float tw = max(fwidth(t), 1e-4);
        float lips = (1.0 - smoothstep(0.0, tw * 1.5, abs(t - 0.25))) + (1.0 - smoothstep(0.0, tw * 1.5, abs(t - 0.75)));
        col = mix(col, INK, clamp(lips, 0.0, 1.0) * 0.8);
        col *= mix(1.0, 0.72, smoothstep(0.1, 0.3, field(q + uLight * 0.35).b - t));
        // Against the walls: ambient occlusion and the shadow cast from the NW.
        col *= 0.6 + 0.4 * smoothstep(0.0, 0.75, d);
        col *= mix(0.66, 1.0, smoothstep(0.35, 0.6, field(q + uLight * 0.45).r));
        // Liquid down the channel.
        float l = here.g;
        if (uLiquid > 0.5) {
            float lw = max(fwidth(l), 1e-3);
            float inside = smoothstep(0.5 - lw, 0.5 + lw, l);
            vec3 liq;
            if (uLiquid < 1.5) {
                float n1 = vnoise(p * 1.4 + vec2(uTime * 0.22, uTime * 0.09));
                float n2 = vnoise(p * 4.2 - vec2(uTime * 0.35, 0.0));
                liq = vec3(0.21, 0.24, 0.14) * (0.78 + 0.4 * n1);
                liq += vec3(0.14, 0.15, 0.07) * smoothstep(0.72, 0.86, n2);
                liq += vec3(0.08) * smoothstep(0.9, 0.97, vnoise(p * 7.0 + uTime * 0.5));
                // Wet grime on the walkway's edge.
                col *= mix(1.0, 0.8, smoothstep(0.15, 0.45, l));
            } else {
                float n1 = vnoise(p * 1.2 + vec2(uTime * 0.05, uTime * 0.03));
                liq = mix(vec3(0.92, 0.26, 0.05), vec3(1.0, 0.78, 0.25), n1);
                float crust = smoothstep(0.58, 0.7, vnoise(p * 2.3 + uTime * 0.04));
                liq = mix(liq, vec3(0.16, 0.08, 0.05), crust * 0.85);
                // Glow on the floor beside it.
                col += vec3(0.5, 0.17, 0.03) * smoothstep(0.08, 0.5, l) * 0.7;
            }
            col = mix(col, liq, inside);
            col = mix(col, INK, (1.0 - smoothstep(0.0, lw * 2.0, abs(l - 0.5))) * 0.55);
        }
        // The 5-ft grid.
        if (uGhost < 0.5) {
            vec2 gq = min(fract(q), 1.0 - fract(q));
            float gw = length(fwidth(q));
            col = mix(col, INK, (1.0 - smoothstep(0.0, gw, min(gq.x, gq.y))) * uGridAlpha);
        }
        alpha = 1.0;
    }
    // The wall's inked outline.
    col = mix(col, INK, (1.0 - smoothstep(0.0, aa * 1.6, abs(d))) * 0.85);
    // A neighbouring section: its floors and their rims only, faint.
    if (uGhost > 0.5) alpha *= d < -rim ? 0.0 : 0.42;
    finalColor = vec4(col * alpha, alpha) * uColor;
}
